
apply plugin: 'java'
apply plugin: 'groovy'

apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: "io.spring.dependency-management"
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.artifactory'

group = 'com.ebsco.platform.training.tdd'
sourceCompatibility = 1.8

repositories {
                mavenCentral()
                mavenLocal()
                maven { url "http://repo.spring.io/snapshot" }
                maven { url "http://repo.spring.io/milestone" }
                maven { url "http://repo.spring.io/release" }
                maven {
					url "${artifactory_contextUrl}/libs-release-local"
					credentials {
					username = project.artifactory_user
					password = project.artifactory_password
					}
				}
}


ext {
        springCloudVersion = 'Edgware.BUILD-SNAPSHOT'
}

def isSnapshot = project.version.endsWith('SNAPSHOT')
def repo = version.endsWith('SNAPSHOT') ? 'libs-snapshot-local' : 'libs-release-local'


artifactory {
    //The base Artifactory URL if not overridden by the publisher/resolver
	contextUrl = project.artifactory_contextUrl

	resolve {
		repository {
			repoKey = 'libs-release'
			username = project.artifactory_user
			password = project.artifactory_password
		}
	}

	publish {
		repository {
			repoKey = repo
			username = project.artifactory_user
			password = project.artifactory_password
			maven = true
		}

		defaults { publications ('mavenJava') }
	}
}
dependencies {
        compile('org.springframework.boot:spring-boot-starter-actuator')
        compile('org.springframework.boot:spring-boot-starter-web')
        compile("org.springframework.boot:spring-boot-devtools")
        testCompile('org.springframework.boot:spring-boot-starter-test')
}

dependencyManagement {
        imports {
                mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
}

task sourcesJar ( type: Jar, dependsOn: classes ) {
	classifier 'sources'
	from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

// usage: gradle artifactoryPublish
// usage: gradle publishToMavenLocal
// usage: gradle publish
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
			artifact sourcesJar
			artifact javadocJar
		}
	}
}

test {
        testLogging {
                exceptionFormat = 'full'
        }
}
task wrapper(type: Wrapper) {
    gradleVersion = '3.5'
}


clean.doFirst {
        delete 'target/'
}
